# Jenkins Shared Library - 클래스(Class)

## Jenkins Shared Library 내 클래스(src/)

```
shared-library/
├── src/
   └── org/company/build/Deploy.groovy  # 클래스 기반 로직
```
- Groovy 또는 Java로 작성된 클래스(Class) 파일을 뜻하며, src/ 디렉토리 내에 저장하여 사용

- 복잡한 비즈니스 로직, 유틸리티 함수, 재사용 가능한 객체 등을 구조적으로 관리 가능

- 전역함수는 `스크립트 형식(Scripted Groovy)`으로 작성하며, 클래스는 `클래스 기반 Groovy(OO Groovy)` 형식으로 작성 
  - 이로 인해 클래스는 표준 Java/Groovy 프로젝트처럼 패키지 구조를 가질 수 있음  
  - 전역함수 혹은 Jenkinsfile에서 Import하여 사용하며 객체 지향 프로그래밍 가능

### Class의 주요 장점
- 코드 재사용성 및 모듈화
  - 반복되는 코드를 줄이고, 유지보수가 용이해짐

- 네임스페이스 및 충돌 방지
  - src 디렉토리 내 패키지 구조를 활용해 네임스페이스를 분리하므로, 동일 명칭의 함수 혹은 클래스 충돌을 방지 

- 글로벌 변수(vars/)와의 역할 분리 가능 
  - 전역 함수(vars/)는 Pipeline에서 간단히 호출할 수 있는 진입점 역할로 사용
  - 클래스(src/)에서 실제 비즈니스 로직 혹은 복잡한 처리를 담당 
  - 명확하게 역할 분리가 가능해짐 

- 구조적이고 체계적인 코드 관리
  - 기능별, 도메인별로 클래스를 나누어 src 디렉토리 하위에 패키지 구조로 정리가능
    - 코드의 가독성, 유지보수성, 확장성 향상  

- 테스트 및 재사용성 증가
  - Class는 Groovy의 일반 코드이므로 CI/CD 파이프라인 외부에서도 독립적으로 테스트 및 검증이 가능 EX) JUnit
    - 전역함수는 Jenkins에서만 동작하므로 별도 단위테스트가 어려움


## Classs 구성 및 사용 시 유의 사항

### 클래스 구성 시 도메인 기반 명명 규칙을 사용하여 작성 
  - 충돌 방지 및 구조화된 네임스페이스를 위해 사용
  - EX) src/org/example/Helper.groovy -> org.example.Helper 
    ```
    src/
    └── org
        ├── example
        │   ├── Helper.groovy
        │   └── Utils.groovy
        └── jenkinslib
            ├── node
            │   └── NodeInfo.groovy
            ├── slack
            │   └── NotifySlack.groovy
            └── util
                └── CommonUtils.groovy  
    ```


### 클래스 구성시 직렬화(Serialization) 선언 필요
- 빌드가 대기상태로 돌입, Jenkins 재시작, 외부 입력 대기 등의 상태로 들어가능 경우 파이프라인의 실행상태를 안전하게 저장 필요
 - 이때 사용되는 객체가 직렬화(Serialization) 할수 있어야 Jenkins 가 이를 디스크에 저장하고 추후에 다시 읽어서 파이프라인을 실행할 수 있음 

- 직렬화 선언 예시
```groovy
package org.jenkinslib

// Class 직렬화 선언 구문
class NodeInfo implements Serializable {

    def script  // Pipeline에서 전달받은 context(this)

	// 생성자: pipeline context(this)를 받아 저장
    NodeInfo(script) {
        this.script = script
    }
......
}
```   

#### 직렬화가 필요 없는 대상
1. 해당 객체가 단순 입력값을 받아 결과를 반환하는 정적 매서드(Static Method)
    - EX) 단순 수학 계산 클래스 
      ```groovy
      // src/org/example/MathUtils.groovy
      package org.example

      class MathUtils {
        static int add(int a, int b) {
          return a + b
        }
      }
      ```

2. 여러 정적 메서드(static method)로만 구성된 유틸리티 클래스
    - EX) 문자열 유틸리티 클래스
      ```groovy
      // src/org/example/StringUtils.groovy
      package org.example

      class StringUtils {
          // 받은 리스트를 ','로 구분하여 리턴
          // ex) def result = StringUtils.joinWithComma(['a', 'b', 'c'])
          static String joinWithComma(List<String> items) {
              return items.join(',')
          }

          // 받은 문자열을 대문자로 변환하여 리턴
          static String toUpper(String input) {
              return input?.toUpperCase()
          }
      }
      ```

3. 상수(static final)만 정의된 클래스
    - EX) 변경되지않는 값을 정의하는 상수 클래스
    ```groovy
    // src/org/example/Constants.groovy
    package org.example

    class Constants {
      static final String DEFAULT_ENV = 'production'
    }
    ```

### Jenkinsfile 내에서 Class 인스턴스화 및 사용
- Jenkinsfile 에서 Jenkins Shared Library의 클래스를 사용하려면 반드시 Script 블록 내에서 인스턴스를 생성 후에 Method를 호출해야함
  ```groovy
  @Library('my-shared-library') _

  // Class Import
  import org.example.Greeting
  
  pipeline {
      agent any
      stages {
          stage('Greet') {
              steps {
                  script {
                      // 클래스 인스턴스화 (객체 생성)
                      def greeter = new Greeting(this)
                      // 메서드 호출
                      greeter.sayHello('Jenkins User')
                  }
              }
          }
      }
  }
  ```


### `Script` 블록 내에서만 사용 가능

- Jenkinsfile에서 클래스를 사용하려면 반드시 script 블록 안에서 인스턴스를 생성하고 메서드를 호출해야 함
  - 만약 `steps` 블록에서 직접 시도하면 오류 발생 
  - script 블록에서만 조건문/반복문/변수선언/클래스 인스턴스화 기능을 사용가능
  - steps 블록은 Jenkins의 내장 스텝 (echo, sh, checkout) 과 플러그인 스텝 만을 순차 실행 가능하도록 설계됨


### Pipeline context
- Jenkins Pipeline 실행 환경에서 제공하는 내장 객체(steps, this, script 등)를 의미

- Shared Library의 클래스를 Jenkinsfile에서 인스턴스화해서 사용 시,
Jenkins의 내장 기능(스텝)을 클래스 내부에서 호출하려면 context 객체가 필요
  - EX) Class 내의 함수가 echo, sh , libraryResource 등의 Jenkins 내장 스텝을 사용하는 경우
  - context를 전달하지 않으면, 클래스 내부에서 Jenkins 스텝을 사용할 수 없고,
파이프라인과의 연동이 불가

```groovy
// Shared Library 사용 선언
@Library('jenkins_shared_library') _

// example.Utils 클래스 import
import org.example.Utils                

// Import 한 변수를 전역으로(여러 Stage) 사용하기 위해 변수 선언
def utils 

pipeline {
    agent any
    stages {
        stage ('Init Classes'){
            steps{
                script{
                   // Jenkins Pipeline context(this)를 Utils 클래스 생성자에 전달하여 utils 인스턴스 생성
                    utils = new Utils(this)
                }
            }
        }
```

### 참고 - def/void
- Mehtod 선언 시  반환값이 있다면 def, 없다면 void로 선언할 수 있음
  - Jenkins Pipeline / Groovy 에서는 굳이 구분하지는않지만, 반환값이 없다는 의미로서 구분하여 쓸수 있음
  - `steps/script.input` 이나 `return` 값이 존재하는 Method는 def만 가능 
  - 

- EX) def로 써야하는 Method
  ```groovy
    // Return 값이 있으므로 def로 써야함 //
    def timestamp(String format = "yyyy-MM-dd HH:mm:ss") {
        return new Date().format(format, TimeZone.getTimeZone('Asia/Seoul'))
    }  
  ```

- EX) void를 쓸수있는 Method
  ```groovy
    // 별도의 반환값이 없으므로 void 사용 가능 //
    // script.echo는 콘솔이나 쉘에 출력하는 것이지 반환값을 주는것이 아님 //
    void cleanWorkspace() {
        script.sh 'rm -rf * .[^.]* || true'
        script.echo "Workspace cleaned."
    } 
  ```


### Class를 Wrapper 함수 만들어 사용 가능
- 만약 사용자에게 내부에서 어떤 작업이 실행되는지 알리고 싶지않거나, 절대 변하지 않고 고정적으로 실행하는 Method가 있다면 Class를 Wrapper 함수로 만들어서 호출하는 형태를 고려해볼 수 있음.

- 그러나 해당 방식을 사용한다면 Stage별로 구분이 어려워지므로 디버깅 문제가 발생할 수 있고, 확장성/유연성이 떨어지는 결과가 생길수 있으므로 사용에 주의할 것


## Jenkins Shared Library Class 구성 예시


#### src/org/example/Helper.groovy

- 특정 문자열 값을 받아 'Hi, ${문자열}' 형태를 Return하는 Class 작성

    ```groovy
    package org.example

    class Helper {
        static String sayHi(String name) {
            return "Hi, ${name}!"
        }
    }
    ```

#### vars/hello.groovy
- 해당 Class를 전역함수에서 사용하는 경우 

    ```groovy    
    import org.example.Helper

    def call(String name = 'World') {
        echo Helper.sayHi(name)
    }
    ```

#### Jenkinsfile
- 해당 Class를 Pipeline 스크립트에서 Import 하여 사용하는 경우
  ```groovy
  @Library('jenkins_shared_library') _  // Shared Library 등록

  pipeline {
      agent any
      stages {
          stage('Say Hello') {
              steps {
                  // Shared Library의 vars/hello.groovy를 호출
                  hello('Jenkins')
              }
          }
      }
  }
  ```    

## Jenkins Shared Library Class 구성 #1 - 유사한 성격의 함수를 묶어 하나의 클래스로 구성

### Class 디렉토리 구성
```
src/
└── org
    └── jenkinslib
        ├── node
        │   └── NodeInfo.groovy
        ├── slack
        │   └── NotifySlack.groovy
        └── util
            └── CommonUtils.groovy
```

### src/org/jenkinslib/node/NodeInfo.groovy
- 노드 정보 확인용 클래스 
```groovy
package org.jenkinslib

class NodeInfo implements Serializable {

    def script  // Pipeline에서 전달받은 context(this)

	// 생성자: pipeline context(this)를 받아 저장
    NodeInfo(script) {
        this.script = script
    }

    /* 현재 노드의 시스템 정보 출력 */
    void printNodeInfo() {
        try {
            script.sh '''
            echo "### Node Info ###"
            echo "- Hostname : $(hostname) , IP : $(hostname -i | awk '{print $1}') User : $(whoami)"
            echo "- Work_dir : $(pwd)"
            echo "- Work dir File list"
            ls -alh
            '''
        } catch (Exception e) {
            script.echo "Failed to print node info: ${e.message}"
            // 실패해도 빌드 중단하지 않도록 예외를 다시 던지지 않음
        }
    }

    /* CPU 코어 수 확인 */
    def getCPUCount() {
        return script.sh(script: "nproc", returnStdout: true).trim()
    }

    /* 노드의 총 메모리(MB) 확인 */
    def getTotalMemoryMB() {
        return script.sh(script: "free -m | awk '/Mem:/ {print \$2}'", returnStdout: true).trim()
    }

    /* 현재 실행 중인 노드의 라벨 목록 반환 */
    def getNodeLabels() {
        return script.env.NODE_LABELS?.split() ?: []
    }

    /* GPU 노드 여부 확인 (NVIDIA 드라이버 존재 여부) */
    def isGPUNode() {
       return script.sh(script: 'command -v nvidia-smi', returnStatus: true) == 0
    }  
}
```

<br>

### src/org/jenkinslib/slack/NotifySlack.groovy
- Slack 메시지 발송용  Class

```groovy
package org.jenkinslib

class NotifySlack implements Serializable {

    def script  // Pipeline에서 전달받은 context(this)

	// 생성자: pipeline context(this)를 받아 저장
    NotifySlack(script) {
        this.script = script
    }

    /* Slack 채널로 메시지 전송 */
    void notifySlackMsg(String channel, String customMessage = '') {
        def baseMessage = "Slack Message"
        def msg = customMessage ? "${customMessage}" : baseMessage
        
        script.slackSend(channel: channel, color: 'good', message: msg)
    }
    
    /* Slack 채널로 메시지 전송 - 빌드 성공시에만 */
    void notifySlackSucc(String channel, String customMessage = '') {
        def baseMessage = ":hammer_and_wrench: Build Info: *${script.env.JOB_NAME} #${script.env.BUILD_NUMBER}*\n:globe_with_meridians:<${script.env.BUILD_URL}|View Result On Jenkins>"
        def msg = customMessage ? "${customMessage}\n${baseMessage}" : baseMessage
        
        script.slackSend(channel: channel, color: 'good', message: msg)
    }

    /* Slack 채널로 메시지 전송 - 빌드 실패시에만 */
    void notifySlackFail(String channel, String customMessage = null) {
        def fullLog = script.currentBuild.rawBuild.getLog(150)
        def filteredLog = fullLog.findAll { line -> !(line.startsWith('[Pipeline]') || line.startsWith('+')) }
        def lastline = filteredLog.takeRight(15).join('\n')
        def rebuildUrl = "${script.env.BUILD_URL}rebuild"
        def baseMessage = """\n:hammer_and_wrench: Build Info: *${script.env.JOB_NAME} #${script.env.BUILD_NUMBER}*\n:mag: *Last Logs* (tail 15)\n```${lastline}\n```\n<${rebuildUrl}|*:repeat: Rebuild Now*>\n\n:globe_with_meridians:<${script.env.BUILD_URL}|View Result On Jenkins>"""
        def msg = customMessage ? "${customMessage}\n${baseMessage}" : ":exclamation:[CI/CD] - 배포 오류\n${baseMessage}"

        script.slackSend(channel: channel, color: 'danger', message: msg)
    }

    /* Slack 채널로 빌드 승인 확인 요청 발송 */
    def notifySlackApproval(String channelID) {
        def jobName = script.env.JOB_NAME ?: 'Unknown Job'
        def buildNumber = script.env.BUILD_NUMBER ?: 'Unknown Build'
        def buildUrl = script.env.BUILD_URL ?: ''

        def message = """
        :rocket: *배포 승인 요청*
        프로젝트: `${jobName}`
        빌드번호: `#${buildNumber}`
        상세정보: ${buildUrl}/input
        """

        script.slackSend(channel: channelID, message: message, color: '#36a64f')  // 초록색 (원하는 색으로 변경 가능)
        script.input message: '배포하시겠습니까?', ok: '승인'
    }    
}
```

<br>

### src/org/jenkinslib/util/CommonUtils.groovy

- 유틸성 함수 모음 Class

```groovy
package org.jenkinslib

class CommonUtils implements Serializable {

    def script  // Pipeline에서 전달받은 context(this)

	// 생성자: pipeline context(this)를 받아 저장
    CommonUtils(script) {
        this.script = script
    }

    // 현재 시간 문자열 반환 (빌드 로그, 슬랙 메시지 등에 유용) //
    def timestamp(String format = "yyyy-MM-dd HH:mm:ss") {
        return new Date().format(format, TimeZone.getTimeZone('Asia/Seoul'))
    }


    // 랜덤 문자열 생성 (임시 이름, 파일, 리소스 이름 등에 활용) //
    def generateRandomSuffix(int length = 6) {
        def charset = ('a'..'z') + ('0'..'9')
        Random rand = new Random()
        return (1..length).collect { charset[rand.nextInt(charset.size())] }.join()
    }

    // 주말 여부 확인 //
    def isWeekend() {
        def today = new Date()
        def dayOfWeek = today.format("u") // Monday = 1, Sunday = 7
        return (dayOfWeek == "6" || dayOfWeek == "7")
    }

    // 민감한 문자열 마스킹 (예: 토큰, 패스워드 일부 출력) //
    def maskString(String input, int visible = 4) {
        if (input.length() <= visible) return '*' * input.length()
        return '*' * (input.length() - visible) + input[-visible..-1]
    }

    // 쉘 명령 실행 및 결과 반환 (try/catch 포함) //
    def runShell(String command) {
        try {
            return script.sh(script: command, returnStdout: true).trim()
        } catch (e) {
            script.echo "[ERROR] Shell command failed: ${e.message}"
            return null
        }
    }


    /* Jenkins 기본 변수 출력용 함수 */
    void echoDefaultEnvironment() {
        script.echo "==== Jenkins Core Variables ===="
        script.echo "JENKINS_HOME: ${script.env.JENKINS_HOME}"
        script.echo "JENKINS_URL: ${script.env.JENKINS_URL}"
        script.echo "JOB_NAME: ${script.env.JOB_NAME}"
        script.echo "JOB_BASE_NAME: ${script.env.JOB_BASE_NAME}"
        script.echo "JOB_URL: ${script.env.JOB_URL}"
        script.echo "BUILD_NUMBER: ${script.env.BUILD_NUMBER}"
        script.echo "BUILD_ID: ${script.env.BUILD_ID}"
        script.echo "BUILD_TAG: ${script.env.BUILD_TAG}"
        script.echo "BUILD_URL: ${script.env.BUILD_URL}"
        script.echo "EXECUTOR_NUMBER: ${script.env.EXECUTOR_NUMBER}"
        script.echo "NODE_NAME: ${script.env.NODE_NAME}"
        script.echo "NODE_LABELS: ${script.env.NODE_LABELS}"
        script.echo "WORKSPACE: ${script.env.WORKSPACE}"

        script.echo "\n==== Git Variables (if SCM is Git) ===="
        script.echo "GIT_COMMIT: ${script.env.GIT_COMMIT}"
        script.echo "GIT_PREVIOUS_COMMIT: ${script.env.GIT_PREVIOUS_COMMIT}"
        script.echo "GIT_BRANCH: ${script.env.GIT_BRANCH}"
        script.echo "GIT_LOCAL_BRANCH: ${script.env.GIT_LOCAL_BRANCH}"
        script.echo "GIT_URL: ${script.env.GIT_URL}"
        script.echo "GIT_AUTHOR_NAME: ${script.env.GIT_AUTHOR_NAME}"
        script.echo "GIT_AUTHOR_EMAIL: ${script.env.GIT_AUTHOR_EMAIL}"

        script.echo "\n==== Pull Request Variables (if Multibranch) ===="
        script.echo "CHANGE_ID: ${script.env.CHANGE_ID}"
        script.echo "CHANGE_URL: ${script.env.CHANGE_URL}"
        script.echo "CHANGE_TITLE: ${script.env.CHANGE_TITLE}"
        script.echo "CHANGE_AUTHOR: ${script.env.CHANGE_AUTHOR}"
        script.echo "CHANGE_AUTHOR_DISPLAY_NAME: ${script.env.CHANGE_AUTHOR_DISPLAY_NAME}"
        script.echo "CHANGE_BRANCH: ${script.env.CHANGE_BRANCH}"
        script.echo "CHANGE_TARGET: ${script.env.CHANGE_TARGET}"

        script.echo "\n==== System Environment ===="
        script.echo "HOME: ${script.env.HOME}"
        script.echo "PATH: ${script.env.PATH}"
        script.echo "SHELL: ${script.env.SHELL}"
        script.echo "USER: ${script.env.USER}"
        script.echo "PWD: ${script.env.PWD}"

        script.sh 'printenv | sort'
    }

    /* 특정 파일 내 key=value 형태의 환경 변수를 파싱하여 Map으로 반환 */     
    def loadVariables(String file) {
        return script.readFile(file)
            .split('\n')
            .findAll { it && !it.startsWith('#') &&  it.contains('=') }
            .collectEntries { line ->
                def (key, value) = line.split('=', 2)
                [(key.trim()): value.trim()]
            }
    }

    // Shared Library 리소스 로드용 - 지정한 리소스파일을 읽어 content에 할당
    // * libraryResource로 리소스 로드시 파일 내 문자열을 리턴하므로 문자열을 파싱하는 별도 함수를 사용
    // * 기존 함수(loadVariables)는 readfile 로 인해 리소스내의 문자열을 파일명으로 인식하므로 사용 불가
    def loadVariablesFromString(String content) {
        return content
            .split('\n')
            .findAll { it && !it.startsWith('#') && it.contains('=') }
            .collectEntries { line ->
                def (key, value) = line.split('=', 2)
                [(key.trim()): value.trim().replaceAll(/^['"]|['"]$/, '')]  // 따옴표 제거
            }
    }

    //  Shared Library 리소스 로드용 - 리소스 파일을 지정하여 loadVariablesFromString를 호출 
    def loadEnvVariables(String resourcePath) {
        def content = script.libraryResource(resourcePath)
        return loadVariablesFromString(content)
    }


    // 워크스페이스 클린업 (디버깅 전용 or 필요 시) //
    void cleanWorkspace() {
        script.sh 'rm -rf * .[^.]* || true'
        script.echo "Workspace cleaned."
    }        
}
```

## Jenkins Shared Library Class 구성 #2 - docker 이미지 리빌드 & 컨테이너 재배포 용 Class
* - 구성 후 업데이트 예정